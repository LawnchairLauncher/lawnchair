plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
}

android {
    namespace "com.android.systemui.unfold"
    buildFeatures {
        aidl true
    }
    sourceSets {
        main {
            java.srcDirs = ['src']
            aidl.srcDirs = ['src']
        }
    }
    ksp {
        arg("dagger.hilt.disableModulesHaveInstallInCheck", "true")
    }
}

addFrameworkJar('framework-14.jar')
compileOnlyCommonJars()

// Workaround for https://github.com/google/dagger/issues/4158
androidComponents {
    onVariants(selector().all(), { variant ->
        afterEvaluate {
            def capName = variant.name.capitalize()
            tasks.getByName("ksp${capName}Kotlin") {
                setSource(tasks.getByName("compile${capName}Aidl").outputs)
            }
        }
    })
}

dependencies {
    implementation "com.google.dagger:hilt-android:$daggerVersion"
    ksp "com.google.dagger:hilt-compiler:$daggerVersion"

    implementation "androidx.concurrent:concurrent-futures:1.2.0"
    implementation "androidx.lifecycle:lifecycle-common:2.8.5"
    implementation "androidx.dynamicanimation:dynamicanimation:1.0.0"
}
