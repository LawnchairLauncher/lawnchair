import com.android.build.api.dsl.ManagedVirtualDevice

plugins {
    id 'com.android.test'
    id 'androidx.baselineprofile'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'app.lawnchair.baseline'

    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    targetProjectPath = ":"

    flavorDimensions += ["app", "recents", "channel"]
    productFlavors {
        lawn { dimension = "app" }
        withQuickstep { dimension = "recents" }
        github { dimension = "channel" }
        market { dimension = "channel" }
    }

    testOptions.managedDevices.devices {
        pixel6Api33(ManagedVirtualDevice) {
            device = "Pixel 6"
            apiLevel = 33
            systemImageSource = "google"
        }
    }
}

// This is the configuration block for the Baseline Profile plugin.
// You can specify to run the generators on a managed devices or connected devices.
baselineProfile {
    managedDevices += "pixel6Api33"
    useConnectedDevices = false
}

dependencies {
    implementation 'androidx.test.ext:junit:1.2.1'
    implementation 'androidx.test.espresso:espresso-core:3.6.1'
    implementation 'androidx.test.uiautomator:uiautomator:2.3.0'
    implementation 'androidx.benchmark:benchmark-macro-junit4:1.3.3'
}
