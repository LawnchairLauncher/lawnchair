plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.protobuf'
}

android {
    namespace "com.android.app.viewcapture"
    sourceSets {
        main {
            java.srcDirs = ['src']
            manifest.srcFile 'AndroidManifest.xml'
            proto.srcDirs = ['src']
        }
        androidTest {
            java.srcDirs = ["tests"]
            manifest.srcFile "tests/AndroidManifest.xml"
        }
    }


    addFrameworkJar('android.jar')
}

dependencies {
    compileOnly fileTree(dir: FRAMEWORK_PREBUILTS_DIR, include: 'WindowManager-Shell.jar')
    compileOnly fileTree(dir: FRAMEWORK_PREBUILTS_DIR, include: 'core.jar')
    compileOnly fileTree(dir: FRAMEWORK_PREBUILTS_DIR, include: 'seeder.jar')
    compileOnly fileTree(dir: FRAMEWORK_PREBUILTS_DIR, include: 'SystemUI-core.jar')
    compileOnly fileTree(dir: FRAMEWORK_PREBUILTS_DIR, include: 'SystemUI-statsd.jar')
    implementation "androidx.core:core:1.9.0"
    implementation "com.google.protobuf:protobuf-javalite:$protocVersion"
}

protobuf {
    // Configure the protoc executable
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
    }
    generateProtoTasks {
        all().configureEach { task ->
            task.builtins {
                remove java
                java {
                    option "lite"
                }
            }
        }
    }
}