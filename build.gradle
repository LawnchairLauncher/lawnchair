buildscript {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-alpha5'
    }
}

apply plugin: 'com.android.application'

def getChangelog = { ->
    def commitRange = System.getenv("TRAVIS_COMMIT_RANGE")
    if (commitRange) {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'log', '--format=%s', commitRange
            standardOutput = stdout
        }
        return stdout.toString().trim().replace("\n", "\\n")
    } else {
        return null
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.0'

    signingConfigs {
        travis {
            storeFile file("debug.jks")
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias "Lawnchair"
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 26
        applicationId 'ch.deletescape.lawnchair'
        buildConfigField "String", "MOBILE_CENTER_KEY", "\"" + System.getenv("MOBILE_CENTER_KEY") + "\""
        buildConfigField "boolean", "TRAVIS", System.getenv("TRAVIS") ?: "false"
        buildConfigField "int", "TRAVIS_BUILD_NUMBER", System.getenv("TRAVIS_BUILD_NUMBER") ?: "0"
        buildConfigField "String", "CHANGELOG", "\"${getChangelog()}\""
        if (System.getenv("TRAVIS") == "true") {
            versionCode = Integer.valueOf(System.getenv("TRAVIS_BUILD_NUMBER"))
            versionName = "Travis-" + System.getenv("TRAVIS_BUILD_NUMBER")
        } else {
            versionCode 1
            versionName "prerelease"
        }

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            if (System.getenv("TRAVIS") == "true") {
                minifyEnabled true
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.flags'
                if (System.getenv("TRAVIS_EVENT_TYPE") != "pull_request") {
                    signingConfig signingConfigs.travis
                }
            } else {
                minifyEnabled false
            }
        }
    }

    flavorDimensions "variant"

    productFlavors {
        aosp {
            applicationId 'ch.deletescape.lawnchair'
            testApplicationId 'ch.deletescape.lawnchair.tests'
            dimension "variant"
        }
    }
    sourceSets {
        main {
            res.srcDirs = ['res']
            java.srcDirs = ['src', 'src_config']
            manifest.srcFile "AndroidManifest.xml"
        }
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenCentral()
    google()
    jcenter()
}

dependencies {
    def mobileCenterSdkVersion = '0.10.0'
    def supportLibVersion = '26.0.0-beta2'
    compile "com.microsoft.azure.mobile:mobile-center-analytics:${mobileCenterSdkVersion}"
    compile "com.microsoft.azure.mobile:mobile-center-crashes:${mobileCenterSdkVersion}"
    compile "com.microsoft.azure.mobile:mobile-center-distribute:${mobileCenterSdkVersion}"
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:palette-v7:${supportLibVersion}"
}
