buildscript {
    ext.kotlin_version = '1.2.21'
    repositories {
        mavenCentral()
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.2'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    signingConfigs {
        travis {
            storeFile file("debug.jks")
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias "Lawnchair"
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 27

        if (System.getenv("TRAVIS") == "true") {
            versionCode = Integer.valueOf(System.getenv("TRAVIS_BUILD_NUMBER"))
            versionName = System.getenv("MAJOR_MINOR") + (System.getenv("DRONE") ? "-" : ".") + System.getenv("TRAVIS_BUILD_NUMBER")
        } else {
            versionCode 1
            versionName "2.0"
        }

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
        }

        optimized {
            initWith debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard.flags'
            if (System.getenv("TRAVIS") == "true") {
                if (System.getenv("TRAVIS_EVENT_TYPE") != "pull_request") {
                    signingConfig signingConfigs.travis
                }
            }
        }
    }

    flavorDimensions "tier", "build"
    productFlavors {
        aosp {
            dimension "tier"
            applicationId 'ch.deletescape.lawnchair'
            testApplicationId 'com.android.launcher3.tests'
        }

        l3go {
            dimension "tier"
            applicationId 'com.google.android.apps.nexuslauncher'
            testApplicationId 'com.android.launcher3.tests'
        }

        dev {
            dimension "build"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
        }

        ci {
            dimension "build"
            applicationIdSuffix ".ci"
            versionNameSuffix "-ci"
        }

        plah {
            dimension "build"
            applicationIdSuffix ".plah"
        }
    }
    sourceSets {
        main {
            res.srcDirs = ['res']
            java.srcDirs = ['src']
            aidl.srcDirs = ['src']
            assets.srcDirs = ['assets']
            manifest.srcFile 'AndroidManifest-common.xml'
            proto {
                srcDir 'protos/'
                srcDir 'proto_overrides/'
                srcDir 'proto_pixel/'
            }
        }

        androidTest {
            res.srcDirs = ['tests/res']
            java.srcDirs = ['tests/src']
            manifest.srcFile "tests/AndroidManifest-common.xml"
        }

        aosp {
            java.srcDirs = ['src_flags']
            manifest.srcFile "AndroidManifest.xml"
        }

        l3go {
            res.srcDirs = ['go/res']
            java.srcDirs = ['go/src_flags']
            // Note: we are using the Launcher3 manifest here because the gradle manifest-merger uses
            // different attributes than the build system.
            manifest.srcFile "AndroidManifest.xml"
        }
    }
}

afterEvaluate {
    android.applicationVariants.all { variant ->
        variant.resValue 'string', 'application_id', variant.applicationId
    }
}

repositories {
    mavenCentral()
    jcenter()
    google()
}

final String SUPPORT_LIBS_VERSION = '27.1.0'
dependencies {
    implementation "com.android.support:appcompat-v7:${SUPPORT_LIBS_VERSION}"
    implementation "com.android.support:cardview-v7:${SUPPORT_LIBS_VERSION}"
    implementation "com.android.support:design:${SUPPORT_LIBS_VERSION}"
    implementation "com.android.support:support-v4:${SUPPORT_LIBS_VERSION}"
    implementation "com.android.support:support-dynamic-animation:${SUPPORT_LIBS_VERSION}"
    implementation "com.android.support:recyclerview-v7:${SUPPORT_LIBS_VERSION}"
    implementation "com.android.support:palette-v7:${SUPPORT_LIBS_VERSION}"
    implementation "com.android.support:preference-v7:${SUPPORT_LIBS_VERSION}"
    implementation "com.android.support:preference-v14:${SUPPORT_LIBS_VERSION}"
    implementation 'com.github.florent37:fiftyshadesof:1.0.0'
    implementation 'com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-7'

    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

protobuf {
    // Configure the protoc executable
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3'

        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    remove java
                    javanano {
                        option "java_package=launcher_log_extension.proto|com.android.launcher3.userevent.nano"
                        option "java_package=launcher_log.proto|com.android.launcher3.userevent.nano"
                        option "java_package=launcher_dump.proto|com.android.launcher3.model.nano"
                        option "java_package=search.proto|com.google.android.apps.nexuslauncher.search.nano"
                        option "java_package=smartspace.proto|com.google.android.apps.nexuslauncher.smartspace.nano"
                        option "enum_style=java"
                    }
                }
            }
        }
    }
}

apply plugin: 'kotlin-android-extensions'
